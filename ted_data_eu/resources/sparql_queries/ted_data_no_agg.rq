PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX cccev: <http://data.europa.eu/m8g/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX epd: <http://data.europa.eu/a4g/resource/>
PREFIX time: <http://www.w3.org/2006/time#>

select distinct
?lot
?publication_date
?notice_id
?winner_name # multiple winners per lot
?winner_nuts # multiple nuts per lot
?subcontracting
?lot_ammount
?lot_currency
?place_of_performance
?contract_duration
?main_cpvs # posbile multiple cpvs per lot
?procedure_title
?procedure_type
?awardet_cpb
?ea_technique
?fa_technique
?is_gpa
?using_eu_funds

where {
     values ?publication_date {
            $date_range
    }
    ?lot a epo:Lot.
    ?result_notice a epo:ResultNotice;
                   	epo:hasPublicationDate ?publication_date;
                    epo:hasID / epo:hasIdentifierValue ?notice_id;
                    epo:refersToLot ?lot.


    optional {
        ?lot_award_outcome epo:describesLot ?lot;
                           a epo:LotAwardOutcome;
                           epo:comprisesTenderAwardOutcome ?tender_award_outcome.
        ?tender_award_outcome a epo:TenderAwardOutcome.

        optional {
            ?tender_award_outcome epo:indicatesAwardOfLotToWinner / epo:playedBy ?winner.
            ?winner a org:Organization.
            optional {
                ?winner epo:hasLegalName ?winner_name.
                FILTER(lang(?winner_name)='en')
            }
            optional {
                ?winner cccev:registeredAddress / epo:hasNutsCode ?winner_nuts.
            }
        }
        optional {
            ?tender_award_outcome epo:describesTender / epo:foreseesSubcontracting ?subcontracting .
        }
        optional {
            ?lot_award_outcome epo:hasAwardedValue ?lot_value.
            ?lot_value a epo:MonetaryValue;
                       epo:hasAmountValue ?lot_ammount;
                       epo:hasCurrency ?lot_currency.
        }
    }

    optional {
        ?lot epo:foreseesContractSpecificTerm ?contract_term.
        ?contract_term a epo:ContractTerm .
        optional {
            ?contract_term epo:definesSpecificPlaceOfPerformance / epo:hasNutsCode ?place_of_performance .
        }
        optional {
            ?contract_term epo:definesContractDuration ?contract_duration .
            ?contract_duration a epo:SpecificDuration.
        }
    }
    optional {
        ?lot epo:hasPurpose / epo:hasAdditionalClassification ?main_cpvs.
    }
    optional {
        ?procedure epo:hasProcurementScopeDividedIntoLot ?lot;
                   a epo:Procedure.
        optional {
            ?procedure epo:hasTitle ?procedure_title .
            FILTER(lang(?procedure_title)='en')
        }
        optional {
            ?procedure epo:hasProcedureType ?procedure_type .
        }
        optional {
            ?procedure epo:isSubjectToProcedureSpecificTerm / epo:isAwardedByCPB ?awardet_cpb.
        }
        optional {
            ?procedure epo:usesTechnique ?ea_technique.
            ?ea_technique a epo:EAuctionTechniqueUsage.
        }
        optional {
            ?procedure epo:usesTechnique ?fa_technique.
            ?fa_technique a epo:FrameworkAgreementTechniqueUsage.
        }
        optional {
            ?procedure epo:isCoveredByGPA ?is_gpa.
        }
    }
    optional {
        ?lot epo:isUsingEUFunds ?using_eu_funds .
    }
}