PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX afn: <http://jena.apache.org/ARQ/function#>

select distinct
?SubmissionStatisticalInformationId
?hasReceivedSMETenders
?hasReceivedTenders
?HighestReceivedTenderValue
(afn:localname(?HighestReceivedTenderValueCurrencyURI) as ?HighestReceivedTenderValueCurrency)
?LowestReceivedTenderValue
(afn:localname(?LowestReceivedTenderValueCurrencyURI) as ?LowestReceivedTenderValueCurrency)
?hasEUReceivedTenders
?hasReceivedNonEUTenders
?hasElectronicTenders
where {
    ?NoticeId a epo:ResultNotice;
              epo:hasPublicationDate ?NoticePublicationDate;
              epo:refersToLot ?LotId.

    values ?NoticePublicationDate {
        $date_range
    }
    
    ?LotId a epo:Lot.
    ?LotAwardOutcomeId epo:describesLot ?LotId;
                       a epo:LotAwardOutcome.
    ?SubmissionStatisticalInformationId epo:summarisesInformationForLotAwardOutcome ?LotAwardOutcomeId;
                                        a epo:SubmissionStatisticalInformation.

    optional {
        ?SubmissionStatisticalInformationId epo:hasReceivedSMETenders ?hasReceivedSMETenders .
    }
    optional {
        ?SubmissionStatisticalInformationId epo:hasReceivedTenders ?hasReceivedTenders .
    }
    optional {
        ?SubmissionStatisticalInformationId epo:hasHighestReceivedTenderValue ?HighestReceivedTenderMonetaryValue .
        ?HighestReceivedTenderMonetaryValue a epo:MonetaryValue;
                                            epo:hasAmountValue ?HighestReceivedTenderValue;
                                            epo:hasCurrency ?HighestReceivedTenderValueCurrencyURI.

    }
    optional {
        ?SubmissionStatisticalInformationId epo:hasLowestReceivedTenderValue ?LowestReceivedTenderMonetaryValue .
        ?LowestReceivedTenderMonetaryValue a epo:MonetaryValue;
                                            epo:hasAmountValue ?LowestReceivedTenderValue;
                                            epo:hasCurrency ?LowestReceivedTenderValueCurrencyURI.
    }
    optional {
        ?SubmissionStatisticalInformationId epo:hasEUReceivedTenders ?hasEUReceivedTenders .
    }
    optional {
        ?SubmissionStatisticalInformationId epo:hasElectronicTenders ?hasElectronicTenders .
    }
    optional {
        ?SubmissionStatisticalInformationId epo:hasReceivedNonEUTenders ?hasReceivedNonEUTenders .
    }

}