PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX afn: <http://jena.apache.org/ARQ/function#>

select distinct
?PurposeId
(afn:localname(?hasOriginalCPV) as ?OriginalCPV)
where {
    {
        select ?PurposeId ?hasOriginalCPV where {
            ?NoticeId a epo:ResultNotice;
                      epo:hasPublicationDate ?NoticePublicationDate;
                      epo:refersToProcedure ?ProcedureId.
            values ?NoticePublicationDate {
                $date_range
            }
            ?ProcedureId epo:hasPurpose ?PurposeId.
            ?PurposeId a epo:Purpose;
                       epo:hasMainClassification ?hasOriginalCPV.
        }
    } union {
        select ?PurposeId ?hasOriginalCPV where {
            ?NoticeId a epo:ResultNotice;
                      epo:hasPublicationDate ?NoticePublicationDate;
                      epo:refersToLot ?LotId.
            values ?NoticePublicationDate {
                $date_range
            }
            ?LotId epo:hasPurpose ?PurposeId.
            ?PurposeId a epo:Purpose;
                       epo:hasAdditionalClassification ?hasOriginalCPV.
        }
    }
}