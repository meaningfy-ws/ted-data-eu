PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX afn: <http://jena.apache.org/ARQ/function#>

select distinct
(afn:localname(?Lot) as ?LotId)
(afn:localname(?Procedure) as ?ProcedureId)
?LotTitle
?isUsingEUFunds
?LotEstimatedValue
(afn:localname(?LotEstimatedValueCurrencyURI) as ?LotEstimatedValueCurrency)
?LotDescription
where {
    ?NoticeId a epo:ResultNotice;
              epo:hasPublicationDate ?NoticePublicationDate;
              epo:refersToLot ?Lot.

    values ?NoticePublicationDate {
        $date_range
    }

    ?Lot a epo:Lot.
    ?Procedure epo:hasProcurementScopeDividedIntoLot ?Lot.

    optional{
        ?Lot a epo:Lot.
        {
            optional {
                ?Lot epo:hasTitle ?LotTitleEn.
                filter(lang(?LotTitleEn) = 'en')
            }
            optional {
                SELECT ?Lot (SAMPLE (?LotTitleInternal) as ?LotTitleOther)
                {
                    ?Lot epo:hasTitle ?LotTitleInternal .
                }
                GROUP BY ?Lot order by ?LotTitleInternal
            }
            BIND(COALESCE(?LotTitleEn, ?LotTitleOther) as ?LotTitle)
        }
    }
    optional{
        ?Lot epo:isUsingEUFunds ?isUsingEUFunds.
    }
    optional{
        ?Lot epo:hasEstimatedValue ?MonetaryValue.
        ?MonetaryValue a epo:MonetaryValue;
                       epo:hasAmountValue ?LotEstimatedValue;
                       epo:hasCurrency ?LotEstimatedValueCurrencyURI.
    }
    optional {
        ?Lot a epo:Lot.
        {
            optional {
                ?Lot epo:hasDescription ?LotDescriptionEn.
                filter(lang(?LotDescriptionEn) = 'en')
            }
            optional {
                SELECT ?Lot (SAMPLE (?LotDescriptionInternal) as ?LotDescriptionOther)
                {
                    ?Lot epo:hasDescription ?LotDescriptionInternal .
                }
                GROUP BY ?Lot order by ?LotDescriptionInternal
            }
            BIND(COALESCE(?LotDescriptionEn, ?LotDescriptionOther) as ?LotDescription)
        }
    }
}