PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX cccev: <http://data.europa.eu/m8g/>
PREFIX afn: <http://jena.apache.org/ARQ/function#>
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX cpov: <http://data.europa.eu/m8g/>

select distinct
(concat(str(afn:localname(?ContactPoint)), str(?Email)) as ?ContactPointEmailId)
(afn:localname(?ContactPoint) as ?ContactPointId)
?Email
where {
    ?NoticeId a epo:ResultNotice;
              epo:hasPublicationDate ?NoticePublicationDate;
              epo:refersToProcedure ?ProcedureId.

    values ?NoticePublicationDate {
        $date_range
    }

    ?NoticeId epo:announcesRole ?Role.
    {
        ?Role a epo:Buyer.
    } union {
        ?Role a epo:Winner.
    }
    ?Role epo:playedBy ?OrganizationId.
    ?OrganizationId a org:Organization.

    {
        ?OrganizationId epo:hasPrimaryContactPoint ?ContactPoint.
    }
    union
    {
        ?Role epo:hasContactPointInRole ?ContactPoint .
    }
    ?ContactPoint a cpov:ContactPoint;
                  cccev:email ?Email.
}