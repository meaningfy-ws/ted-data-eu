PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX cccev: <http://data.europa.eu/m8g/>
PREFIX afn: <http://jena.apache.org/ARQ/function#>

select distinct
?OrganizationId
?LegalName
?AddressId
(afn:localname(?hasBusinessSize) as ?BusinessSize)
where {
    ?ResultNotice a epo:ResultNotice;
                   epo:hasPublicationDate ?NoticePublicationDate;
                   epo:refersToProcedure ?ProcedureId.
    values ?NoticePublicationDate {
        $date_range
    }
    optional {
        ?ResultNotice epo:announcesRole ?Role.
        {
            ?Role a epo:Buyer.
        } union {
            ?Role a epo:Winner.
        }
        ?Role epo:playedBy ?OrganizationId.
        optional {
            ?OrganizationId a org:Organization.
            {
                optional {
                    ?OrganizationId epo:hasLegalName ?LegalNameEn .
                    filter(lang(?LegalNameEn) = 'en')
                }
                optional {
                    SELECT ?OrganizationId (SAMPLE (?LegalNameInternal) as ?LegalNameOther)
                    {
                        ?OrganizationId epo:hasLegalName ?LegalNameInternal .
                    }
                    GROUP BY ?OrganizationId order by ?LegalNameInternal
                }
                BIND(COALESCE(?LegalNameEn, ?LegalNameOther) as ?LegalName)
            }
            optional {
                ?OrganizationId cccev:registeredAddress ?AddressId.
            }
            optional {
                ?OrganizationId epo:hasBusinessSize ?hasBusinessSize.
            }
        }
    }
} 