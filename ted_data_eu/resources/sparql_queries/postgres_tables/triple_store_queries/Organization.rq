PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX cccev: <http://data.europa.eu/m8g/>
PREFIX afn: <http://jena.apache.org/ARQ/function#>

select distinct
(afn:localname(?Organization) as ?OrganizationId)
?LegalName
where {
    ?ResultNotice a epo:ResultNotice;
                  epo:hasPublicationDate ?NoticePublicationDate;
                  epo:refersToProcedure ?ProcedureId.
    values ?NoticePublicationDate {
        $date_range
    }

    ?ResultNotice epo:announcesRole ?Role.
    {
        ?Role a epo:Buyer.
    } union {
        ?Role a epo:Winner.
    }
    ?Role epo:playedBy ?Organization.
    optional {
        ?Organization a org:Organization.
        {
            optional {
                ?Organization epo:hasLegalName ?LegalNameEn .
                filter(lang(?LegalNameEn) = 'en')
            }
            optional {
                SELECT ?Organization (SAMPLE (?LegalNameInternal) as ?LegalNameOther)
                {
                    ?Organization epo:hasLegalName ?LegalNameInternal .
                }
                GROUP BY ?Organization order by ?LegalNameInternal
            }
            BIND(COALESCE(?LegalNameEn, ?LegalNameOther) as ?LegalName)
        }
    }
}