PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX afn: <http://jena.apache.org/ARQ/function#>

select distinct
?LotAwardOutcomeId
(sample(afn:localname(?hasAwardStatus)) as ?AwardStatus)
(sample(?LotAwardedValue) as ?LotAwardedValue)
(sample(afn:localname(?LotAwardedValueCurrencyURI)) as ?LotAwardedValueCurrency)
(sample(?LotBargainPrice) as ?LotBargainPrice)
(sample(afn:localname(?LotBargainPriceCurrencyURI)) as ?LotBargainPriceCurrency)
(sample(?AwardDecisionDate) as ?AwardDecisionDate)
(group_concat(?WinnerId; separator="|||") as ?WinnerId)
(sample(?SubmissionStatisticalInformationId) as ?SubmissionStatisticalInformationId)
where {
    ?NoticeId a epo:ResultNotice;
              epo:hasPublicationDate ?NoticePublicationDate;
              epo:refersToLot ?LotId.

    values ?NoticePublicationDate {
        $date_range
    }

    ?LotId a epo:Lot.
    ?LotAwardOutcomeId epo:describesLot ?LotId;
                       a epo:LotAwardOutcome.

    optional {
        ?LotAwardOutcomeId epo:hasAwardStatus ?hasAwardStatus.
    }
    optional {
        ?LotAwardOutcomeId epo:hasAwardedValue ?MonetaryValue.
        ?MonetaryValue a epo:MonetaryValue;
                       epo:hasAmountValue ?LotAwardedValue;
                       epo:hasCurrency ?LotAwardedValueCurrencyURI.
    }
    optional {
        ?LotAwardOutcomeId epo:hasBargainPrice ?BargainPriceMonetaryValue.
        ?BargainPriceMonetaryValue a epo:MonetaryValue;
                       epo:hasAmountValue ?LotBargainPrice;
                       epo:hasCurrency ?LotBargainPriceCurrencyURI.
    }
    optional {
        ?LotAwardOutcomeId epo:hasAwardDecisionDate ?AwardDecisionDate.
    }
    optional {
        ?LotAwardOutcomeId epo:comprisesTenderAwardOutcome / epo:indicatesAwardOfLotToWinner ?WinnerId.
    }
    optional {
        ?SubmissionStatisticalInformationId epo:summarisesInformationForLotAwardOutcome ?LotAwardOutcomeId;
                                            a epo:SubmissionStatisticalInformation.
    }
} group by ?LotAwardOutcomeId