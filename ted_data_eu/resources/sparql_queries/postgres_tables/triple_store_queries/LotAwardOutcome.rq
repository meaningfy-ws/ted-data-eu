PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX afn: <http://jena.apache.org/ARQ/function#>

select distinct
(afn:localname(?LotAwardOutcome) as ?LotAwardOutcomeId)
(afn:localname(?hasAwardStatus) as ?AwardStatus)
?LotAwardedValue
(afn:localname(?LotAwardedValueCurrencyURI) as ?LotAwardedValueCurrency)
?LotBargainPrice
(afn:localname(?LotBargainPriceCurrencyURI) as ?LotBargainPriceCurrency)
?AwardDecisionDate
where {
    ?NoticeId a epo:ResultNotice;
              epo:hasPublicationDate ?NoticePublicationDate;
              epo:refersToLot ?Lot.

    values ?NoticePublicationDate {
        $date_range
    }

    ?Lot a epo:Lot.
    ?LotAwardOutcome epo:describesLot ?Lot;
                       a epo:LotAwardOutcome.

    optional {
        ?LotAwardOutcome epo:hasAwardStatus ?hasAwardStatus.
    }
    optional {
        ?LotAwardOutcome epo:hasAwardedValue ?MonetaryValue.
        ?MonetaryValue a epo:MonetaryValue;
                       epo:hasAmountValue ?LotAwardedValue;
                       epo:hasCurrency ?LotAwardedValueCurrencyURI.
    }
    optional {
        ?LotAwardOutcome epo:hasBargainPrice ?BargainPriceMonetaryValue.
        ?BargainPriceMonetaryValue a epo:MonetaryValue;
                       epo:hasAmountValue ?LotBargainPrice;
                       epo:hasCurrency ?LotBargainPriceCurrencyURI.
    }
    optional {
        ?LotAwardOutcome epo:hasAwardDecisionDate ?AwardDecisionDate.
    }
}