PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX afn: <http://jena.apache.org/ARQ/function#>

select distinct
?ProcedureId
(sample(?ProcedureTitle) as ?ProcedureTitle)
(sample(?ReferenceNumber) as ?ReferenceNumber)
(group_concat(afn:localname(?ProcedureCPVURI); separator="|||") as ?ProcedureCPV)
(sample(?ProcedureEstimatedValue) as ?ProcedureEstimatedValue)
(sample(afn:localname(?ProcedureEstimatedValueCurrencyURI)) as ?ProcedureEstimatedValueCurrency)
(group_concat(?BuyerId; separator="|||") as ?BuyerId)
(sample(afn:localname(?hasProcedureType)) as ?ProcedureType)
(sample(?isCoveredByGPA) as ?isCoveredByGPA)
where {
    ?NoticeId a epo:ResultNotice;
              epo:hasPublicationDate ?NoticePublicationDate;
              epo:refersToProcedure ?ProcedureId;
              epo:announcesRole ?BuyerId.

    values ?NoticePublicationDate {
        $date_range
    }

    ?BuyerId a epo:Buyer.
    ?ProcedureId a epo:Procedure.

    optional {
         {
            ?ProcedureId epo:hasTitle ?ProcedureTitleEn.
            filter(lang(?ProcedureTitleEn) = 'en')
        }
        {
            SELECT ?ProcedureId (SAMPLE (?ProcedureTitleInternal) as ?ProcedureTitleOther)
            {
                ?ProcedureId epo:hasTitle ?ProcedureTitleInternal .
            }
            GROUP BY ?ProcedureId order by ?ProcedureTitleInternal
        }
        BIND(COALESCE(?ProcedureTitleEn, ?ProcedureTitleOther) as ?ProcedureTitle)
    }
    optional {
        ?ProcedureId epo:hasID / epo:hasIdentifierValue ?ReferenceNumber.
    }
    optional {
        ?ProcedureId epo:hasPurpose / epo:hasMainClassification ?ProcedureCPVURI.
    }
    optional {
        ?ProcedureId epo:hasEstimatedValue ?MonetaryValue.
        ?MonetaryValue a epo:MonetaryValue;
        				epo:hasAmountValue ?ProcedureEstimatedValue;
            			epo:hasCurrency ?ProcedureEstimatedValueCurrencyURI.
    }
    optional {
        ?ProcedureId epo:hasProcedureType ?hasProcedureType.
    }
    optional {
        ?ProcedureId epo:isCoveredByGPA ?isCoveredByGPA.
    }

} group by ?ProcedureId