PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX afn: <http://jena.apache.org/ARQ/function#>

select distinct
(afn:localname(?Procedure) as ?ProcedureId)
?ProcedureTitle
?ReferenceNumber
?ProcedureEstimatedValue
(afn:localname(?ProcedureEstimatedValueCurrencyURI) as ?ProcedureEstimatedValueCurrency)
(afn:localname(?hasProcedureType) as ?ProcedureType)
?isCoveredByGPA
?ProcedureDescription
?ProcedureAdditionalInformation
where {
    ?NoticeId a epo:ResultNotice;
              epo:hasPublicationDate ?NoticePublicationDate;
              epo:refersToProcedure ?Procedure.

    values ?NoticePublicationDate {
        $date_range
    }

    optional {
        ?Procedure a epo:Procedure.
        {
            optional {
                ?Procedure epo:hasTitle ?ProcedureTitleEn.
                filter(lang(?ProcedureTitleEn) = 'en')
            }
            optional {
                SELECT ?Procedure (SAMPLE (?ProcedureTitleInternal) as ?ProcedureTitleOther)
                {
                    ?Procedure epo:hasTitle ?ProcedureTitleInternal .
                }
                GROUP BY ?Procedure order by ?ProcedureTitleInternal
            }
            BIND(COALESCE(?ProcedureTitleEn, ?ProcedureTitleOther) as ?ProcedureTitle)
        }
    }
    optional {
        ?Procedure epo:hasID / epo:hasIdentifierValue ?ReferenceNumber.
    }
    optional {
        ?Procedure epo:hasEstimatedValue ?MonetaryValue.
        ?MonetaryValue a epo:MonetaryValue;
                       epo:hasAmountValue ?ProcedureEstimatedValue;
                       epo:hasCurrency ?ProcedureEstimatedValueCurrencyURI.
    }
    optional {
        ?Procedure epo:hasProcedureType ?hasProcedureType.
    }
    optional {
        ?Procedure epo:isCoveredByGPA ?isCoveredByGPA.
    }

    optional {
        ?Procedure a epo:Procedure.
        {
            optional {
                ?Procedure epo:hasDescription ?DescriptionEn.
                filter(lang(?DescriptionEn) = 'en')
            }
            optional {
                SELECT ?Procedure (SAMPLE (?DescriptionInternal) as ?DescriptionOther)
                {
                    ?Procedure epo:hasDescription ?DescriptionInternal .
                }
                GROUP BY ?Procedure order by ?DescriptionInternal
            }
            BIND(COALESCE(?DescriptionEn, ?DescriptionOther) as ?ProcedureDescription)
        }
    }
    optional {
        {
            ?Procedure epo:hasAdditionalInformation ?AdditionalInformationEn.
            filter(lang(?AdditionalInformationEn) = 'en')
        }
        {
            SELECT ?Procedure (SAMPLE (?AdditionalInformationInternal) as ?AdditionalInformationOther)
            {
                ?Procedure epo:hasAdditionalInformation ?AdditionalInformationInternal .
            }
            GROUP BY ?Procedure order by ?AdditionalInformationInternal
        }
        BIND(COALESCE(?AdditionalInformationEn, ?AdditionalInformationOther) as ?ProcedureAdditionalInformation)
    }
}