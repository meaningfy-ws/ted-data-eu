PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX afn: <http://jena.apache.org/ARQ/function#>

select distinct
?ProcedureId
?ProcedureTitle
?ReferenceNumber
?ProcedureEstimatedValue
(afn:localname(?ProcedureEstimatedValueCurrencyURI) as ?ProcedureEstimatedValueCurrency)
(afn:localname(?hasProcedureType) as ?ProcedureType)
?isCoveredByGPA
?ProcedureDescription
?ProcedureAdditionalInformation
where {
    ?NoticeId a epo:ResultNotice;
              epo:hasPublicationDate ?NoticePublicationDate;
              epo:refersToProcedure ?ProcedureId.

    values ?NoticePublicationDate {
        $date_range
    }

    ?ProcedureId a epo:Procedure.

    optional {
        {
            ?ProcedureId epo:hasTitle ?ProcedureTitleEn.
            filter(lang(?ProcedureTitleEn) = 'en')
        }
        {
            SELECT ?ProcedureId (SAMPLE (?ProcedureTitleInternal) as ?ProcedureTitleOther)
            {
                ?ProcedureId epo:hasTitle ?ProcedureTitleInternal .
            }
            GROUP BY ?ProcedureId order by ?ProcedureTitleInternal
        }
        BIND(COALESCE(?ProcedureTitleEn, ?ProcedureTitleOther) as ?ProcedureTitle)
    }
    optional {
        ?ProcedureId epo:hasID / epo:hasIdentifierValue ?ReferenceNumber.
    }
    optional {
        ?ProcedureId epo:hasEstimatedValue ?MonetaryValue.
        ?MonetaryValue a epo:MonetaryValue;
                       epo:hasAmountValue ?ProcedureEstimatedValue;
                       epo:hasCurrency ?ProcedureEstimatedValueCurrencyURI.
    }
    optional {
        ?ProcedureId epo:hasProcedureType ?hasProcedureType.
    }
    optional {
        ?ProcedureId epo:isCoveredByGPA ?isCoveredByGPA.
    }

    optional {
         {
            ?ProcedureId epo:hasDescription ?DescriptionEn.
            filter(lang(?DescriptionEn) = 'en')
        }
        {
            SELECT ?ProcedureId (SAMPLE (?DescriptionInternal) as ?DescriptionOther)
            {
                ?ProcedureId epo:hasDescription ?DescriptionInternal .
            }
            GROUP BY ?ProcedureId order by ?DescriptionInternal
        }
        BIND(COALESCE(?DescriptionEn, ?DescriptionOther) as ?ProcedureDescription)
    }
    optional {
        {
        ?ProcedureId epo:hasAdditionalInformation ?AdditionalInformationEn.
            filter(lang(?AdditionalInformationEn) = 'en')
        }
        {
            SELECT ?ProcedureId (SAMPLE (?AdditionalInformationInternal) as ?AdditionalInformationOther)
            {
                ?ProcedureId epo:hasAdditionalInformation ?AdditionalInformationInternal .
            }
            GROUP BY ?ProcedureId order by ?AdditionalInformationInternal
        }
        BIND(COALESCE(?AdditionalInformationEn, ?AdditionalInformationOther) as ?ProcedureAdditionalInformation)
    }


}