PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX afn: <http://jena.apache.org/ARQ/function#>
PREFIX cccev: <http://data.europa.eu/m8g/>
PREFIX dcterms: <http://purl.org/dc/terms/>

select distinct
?AddressId
(afn:localname(?hasCountryCode) as ?CountryCode)
(afn:localname(?hasNUTSId) as ?NUTSId)
?PostCode
?PostName
?Thoroughfare
# Temporary disabled
#?FullAddress
where {

    ?NoticeId a epo:ResultNotice;
              epo:hasPublicationDate ?NoticePublicationDate;
              epo:refersToProcedure ?ProcedureId.

    values ?NoticePublicationDate {
        $date_range
    }

    {
        ?NoticeId epo:announcesRole / epo:playedBy ?Organization.
        ?Organization cccev:registeredAddress ?AddressId.
    	?AddressId a locn:Address.
    } union
    {
        ?NoticeId epo:refersToLot ?LotId.
        ?LotId a epo:Lot;
               epo:foreseesContractSpecificTerm / epo:definesSpecificPlaceOfPerformance ?AddressId .
        ?AddressId a dcterms:Location .
    }


    optional {
        ?AddressId epo:hasCountryCode ?hasCountryCode .
    }
    optional {
        ?AddressId epo:hasNutsCode ?hasNUTSId.
    }
    optional {
        ?AddressId locn:postCode ?PostCode.
    }
    optional {
        ?AddressId locn:postName ?PostName.
    }
    optional {
        ?AddressId locn:thoroughfare ?Thoroughfare.
    }
# Temporary disabled
#    optional {
#        ?AddressId locn:address ?FullAddress.
#    }
}