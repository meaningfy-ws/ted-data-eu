PREFIX epo: <http://data.europa.eu/a4g/ontology#>
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX cccev: <http://data.europa.eu/m8g/>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX epd: <http://data.europa.eu/a4g/resource/>
PREFIX time: <http://www.w3.org/2006/time#>

select distinct
?lot

(sample(?publication_date) as ?publication_date)
(GROUP_CONCAT(distinct str(?buyer_name); SEPARATOR=" ||| ") as ?buyer_names)
(GROUP_CONCAT(distinct ?buyer_nuts; SEPARATOR=" ||| ") as ?buyer_nuts) # new
(sample(?procedure) as ?procedure) # new
(sample(?procedure_id) as ?procedure_id) # new
(sample(?procedure_description) as ?procedure_description) # new
(GROUP_CONCAT(distinct str(?winner_name); SEPARATOR=" ||| ") as ?winner_names) # multiple winners per lot
(GROUP_CONCAT(distinct ?winner_nuts; SEPARATOR=" ||| ") as ?winner_nuts)
(sample(?subcontracting) as ?subcontracting)
(sample(?lot_amount) as ?lot_amount)
(sample(?lot_currency) as ?lot_currency)
(sample(?place_of_performance) as ?place_of_performance)
(sample(?contract_duration) as ?contract_duration)
(GROUP_CONCAT(distinct ?main_cpvs; SEPARATOR=" ||| ") as ?main_cpvs) # multiple cpvs per lot
(sample(?procedure_title) as ?procedure_title) # multiple languages
(sample(?procedure_type) as ?procedure_type)
(sample(?awarded_cpb) as ?awarded_cpb)
(sample(?ea_technique) as ?ea_technique)
(sample(?fa_technique) as ?fa_technique)
(sample(?is_gpa) as ?is_gpa)
(sample(?using_eu_funds) as ?using_eu_funds)
where {
    values ?publication_date {
        $date_range
    }
    ?lot a epo:Lot.
    ?result_notice a epo:ResultNotice;
                   epo:hasPublicationDate ?publication_date;
                   epo:refersToLot ?lot.
    optional {
        ?result_notice epo:announcesRole ?buyer.
        ?buyer a epo:Buyer;
               epo:playedBy ?buyer_org.
        optional {
            ?buyer_org a org:Organization.
            optional {
                ?buyer_org cccev:registeredAddress / epo:hasNutsCode ?buyer_nuts.
            }
            optional {
                ?buyer_org epo:hasLegalName ?buyer_name .
                # Temporary solution
                filter(lang(?buyer_name) = 'en')
            }
        }
    }
    optional {
        ?lot_award_outcome epo:describesLot ?lot;
                           a epo:LotAwardOutcome;
                           epo:comprisesTenderAwardOutcome ?tender_award_outcome.
        ?tender_award_outcome a epo:TenderAwardOutcome.

        optional {
            ?tender_award_outcome epo:indicatesAwardOfLotToWinner / epo:playedBy ?winner.
            ?winner a org:Organization.
            optional {
                ?winner epo:hasLegalName ?winner_name.
                FILTER(lang(?winner_name)='en')
            }
            optional {
                ?winner cccev:registeredAddress / epo:hasNutsCode ?winner_nuts.
            }
        }
        optional {
            ?tender_award_outcome epo:describesTender / epo:foreseesSubcontracting ?subcontracting .
        }
        optional {
            ?lot_award_outcome epo:hasAwardedValue ?lot_value.
            ?lot_value a epo:MonetaryValue;
                       epo:hasAmountValue ?lot_amount;
                       epo:hasCurrency ?lot_currency.
        }
    }
    optional {
        ?lot epo:foreseesContractSpecificTerm ?contract_term.
        ?contract_term a epo:ContractTerm .
        optional {
            ?contract_term epo:definesSpecificPlaceOfPerformance / epo:hasNutsCode ?place_of_performance .
        }
        optional {
            ?contract_term epo:definesContractDuration ?contract_duration .
            ?contract_duration a epo:SpecificDuration.
        }
    }
    optional {
        ?lot epo:hasPurpose / epo:hasAdditionalClassification ?main_cpvs.
    }
    optional {
        ?procedure epo:hasProcurementScopeDividedIntoLot ?lot;
                   a epo:Procedure.
        optional {
            ?procedure epo:hasDescription ?procedure_description.
        }
        optional {
            ?procedure epo:hasID / epo:hasIdentifierValue ?procedure_id.
        }
        optional {
            ?procedure epo:hasTitle ?procedure_title .
            #FILTER(lang(?procedure_title)='en')
        }
        optional {
            ?procedure epo:hasProcedureType ?procedure_type .
        }
        optional {
            ?procedure epo:isSubjectToProcedureSpecificTerm / epo:isAwardedByCPB ?awarded_cpb.
        }
        optional {
            ?procedure epo:usesTechnique ?ea_technique.
            ?ea_technique a epo:EAuctionTechniqueUsage.
        }
        optional {
            ?procedure epo:usesTechnique ?fa_technique.
            ?fa_technique a epo:FrameworkAgreementTechniqueUsage.
        }
        optional {
            ?procedure epo:isCoveredByGPA ?is_gpa.
        }
    }
    optional {
        ?lot epo:isUsingEUFunds ?using_eu_funds .
    }
} group by ?lot limit 50